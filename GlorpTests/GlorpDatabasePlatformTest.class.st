Class {
	#name : #GlorpDatabasePlatformTest,
	#superclass : #GlorpTestCase,
	#category : #'GlorpTests-GlorpTest'
}

{ #category : #'VisualWorks metadata' }
GlorpDatabasePlatformTest class >> visualWorksMetadata [

	^ #(
		'namespace' 'Glorp'
		'superclassNamespace' 'Glorp'
	)

]

{ #category : #tests }
GlorpDatabasePlatformTest >> addToString: aString [
	"Append a couple of special characters. The result will utf-8 encode to be 3 characters longer than its number of characters"
	^aString, (String with: (Character value: 16r2022) "bullet point" with: (Character value: 16r0131) "Turkish i with no dot")

"If PostgreSQL, or another DB returning true to measuresStringFieldWidthsInBytes, is provided in VA, this test must alter to
handle incompatible protocol between VW, which lacks Character>>asString and VA, in which String>>with:with: will not handle
double-byte entries.  The VA could be implemented as
	^aString, (Character value: 16r2022) asString, (Character value: 16r0131) asString
"
]

{ #category : #tests }
GlorpDatabasePlatformTest >> helpTestPrintTimestamp: aTimestampString [

	| aStream ts timestamp |
	ts := DatabasePlatform new readTimestamp: aTimestampString for: nil.
	aStream := WriteStream on: String new.
	DatabasePlatform new printDate: ts isoFormatOn: aStream.
	aStream nextPut: Character space.
	DatabasePlatform new printTime: ts isoFormatOn: aStream milliseconds: true.
	timestamp := aStream contents.
	self assert: timestamp = aTimestampString.
]

{ #category : #tests }
GlorpDatabasePlatformTest >> string20 [
	| platform |
	platform := Dialect isVisualAge
		ifTrue:
			[Transcript cr; show: self class name, '>>string20: testing Oracle, not PostgreSQL'.
			OraclePlatform new]
		ifFalse: [PostgresSocketPlatform new].
	platform characterEncoding: #'utf-8'.
	^platform varchar: 20.
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testNumericCoercion [

	| type platform num |
	type := GlorpNumericType new scale: 5; precision: 2.
	platform := DatabasePlatform new.
	num := Dialect coerceToDoublePrecisionFloat: (123456 / 100).
	self assert: (platform convertToNumber: num for: type) = num.
	num := Dialect coerceToDoublePrecisionFloat: (123450 / 100).
	self assert: (platform convertToNumber: num for: type) = num.
	num := Dialect coerceToDoublePrecisionFloat: (123450 / 1000).
	self assert: (platform convertToNumber: num for: type) = num.
	num := Dialect coerceToDoublePrecisionFloat: (123456 / 1000).
	self deny: (platform convertToNumber: num for: type) = num.
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testPrintTimestamp [

	| strings |
	strings := #( '2005-01-01 21:29:28.337'  '2005-01-01 01:02:00.037'  '2005-01-01 21:29:28.002'  '2005-01-01 21:29:28.001').
	strings do: [:each |
		self helpTestPrintTimestamp: each].
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testReadTimestamp [

	| ts |
	ts := DatabasePlatform new readTimestamp: '2003-03-03 15:29:28.337-05' for: nil.
	self assert: ts asSeconds = 3224158168.
	self assert: ([ts asMilliseconds = 3224158168337 ]
		on: MessageNotUnderstood
		do: [ :mnu | mnu return: mnu message selector = #asMilliseconds ]).

	ts := DatabasePlatform new readTimestamp: '2003-03-13 15:29:28.337-05' for: nil.
	self assert: ts asSeconds = 3225022168.
	self assert: ([ts asMilliseconds = 3225022168337]
		on: MessageNotUnderstood
		do: [ :mnu | mnu return: mnu message selector = #asMilliseconds ])
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testReadTimestampNoMS [

	| ts |
	ts := DatabasePlatform new readTimestamp: '2003-03-03 15:29:28-05' for: nil.
	self assert: ts year = 2003.
	self assert: ts monthIndex = 3.
	self assert: ts dayOfMonth = 3.
	self assert: ts hours = 15.
	self assert: ts minutes = 29.
	self assert: ts seconds truncated = 28.
	self assert: ([ ts milliseconds = 0 ]
		on: MessageNotUnderstood
		do: [ :mnu | mnu return: mnu message selector = #milliseconds ])
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testReadTimestampNoMSNoTZ [

	| ts |
	ts := DatabasePlatform new readTimestamp: '2003-03-03 15:29:28' for: nil.
	self assert: ts year = 2003.
	self assert: ts monthIndex = 3.
	self assert: ts dayOfMonth = 3.
	self assert: ts hours = 15.
	self assert: ts minutes = 29.
	self assert: ts seconds truncated = 28.
	self assert: ([ ts milliseconds = 0 ]
		on: MessageNotUnderstood
		do: [ :mnu | mnu return: mnu message selector = #milliseconds ])
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testReadTimestampNoTZ [

	| ts |
	ts := DatabasePlatform new readTimestamp: '1975-08-13 21:29:28.337' for: nil.
	self assert: ts year = 1975.
	self assert: ts monthIndex = 8.
	self assert: ts dayOfMonth = 13.
	self assert: ts hours = 21.
	self assert: ts minutes = 29.
	self assert: ts seconds truncated = 28.
	self assert: ([ ts milliseconds =  337]
		on: MessageNotUnderstood
		do: [ :mnu | mnu return: mnu message selector = #milliseconds ])
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testReadTimestampOverflowDays [

	| ts |
	ts := DatabasePlatform new readTimestamp: '1975-08-13 21:29:28.337-05' for: nil.
	self assert: ts year = 1975.
	self assert: ts monthIndex = 8.
	self assert: ts dayOfMonth = 13.
	self assert: ts hours = 21.
	self assert: ts minutes = 29.
	self assert: ts seconds truncated = 28.
	self assert: ([ ts milliseconds = 337 ]
		on: MessageNotUnderstood
		do: [ :mnu | mnu return: mnu message selector = #milliseconds ])
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testTrimString1 [
	"Normal ascii"
	| type plat |
	type := self string20.
	plat := type platform.
	self assert: (plat trimString: 'abcdefghi' for: type) = 'abcdefghi'.
	self assert: (plat trimString: 'abcdefghijklmnopqrst' for: type) = 'abcdefghijklmnopqrst'.
	self assert: (plat trimString: 'abcdefghijklmnopqrstu' for: type) = 'abcdefghijklmnopqrst'.
	self assert: (plat trimString: 'abcdefghijklmnopqrstuvwxy' for: type) = 'abcdefghijklmnopqrst'.
]

{ #category : #tests }
GlorpDatabasePlatformTest >> testTrimString2 [
	"Some special characters"
	| type plat string |
	Dialect isVisualAge ifTrue: [Transcript cr; show: self class name; show: '>>testTrimString2 skipped in VA'. ^self].

	type := self string20.
	plat := type platform.

	string := self addToString: 'abcdefghi'.
	self assert: (plat trimString: string for: type) = string.
	string := self addToString: 'abcdefghijklmno'.
	self assert: (plat trimString: string for: type) = string.
	string := self addToString: 'abcdefghijklmnop'.
	self assert: (plat trimString: string for: type) = (string copyFrom: 1 to: string size -1).
	string := self addToString: 'abcdefghijklmnopq'.
	self assert: (plat trimString: string for: type) = (string copyFrom: 1 to: string size -1).
	string := (String new: 100000) atAllPut: $a.
	self assert: (plat trimString: string for: type) = 'aaaaaaaaaaaaaaaaaaaa'
]
